// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath 'com.google.gms:google-services:4.3.15'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.8.20'
        classpath 'androidx.navigation:navigation-safe-args-gradle-plugin:2.5.3'
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.41.0'
}

allprojects {
    repositories {
        google()
        mavenCentral()
        mavenLocal()
    }

    def isNonStable = { candidate -> 
        return ['alpha', 'beta', 'rc', 'SNAPSHOT'].any { word ->
            return candidate.version.contains(word)
        }
    }

    def isBlackListed = { candidate ->
        def blacklist = [
            'androidx.browser:browser'
        ]
        return blacklist.any { word ->
            return candidate.toString().contains(word)
        }
    }

    dependencyUpdates {
        rejectVersionIf {
            isNonStable(it.candidate) || isBlackListed(it.candidate)
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
